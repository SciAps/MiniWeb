buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 19
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }

    sourceSets {
        main.java.srcDirs = ['src/main/java', '../miniweb-core/src/main/java']
    }


    buildTypes {
        release {
        }
    }

    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }
}

dependencies {
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'org.slf4j:slf4j-api:1.7.8'

}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts() {
    archives androidSourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories.mavenDeployer {

        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
            name 'Miniweb-core'
            description 'Miniweb is an embedded webserver for Android.'
            url 'http://github.com/dinocore1/MiniWeb'

            scm {
                url 'scm:git@github.com:dinocore1/MiniWeb.git'
                connection 'scm:git@github.com:dinocore1/MiniWeb.git'
                developerConnection 'scm:git@github.com:dinocore1/MiniWeb.git'
            }

            licenses {
                license {
                    name 'MIT License'
                    url 'https://github.com/dinocore1/MiniWeb/blob/master/LICENSE.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'psoucy'
                    name 'Paul Soucy'
                }
            }
        }

    }
}

